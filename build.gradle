import static org.apache.commons.lang3.StringUtils.repeat
buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'net.covers1624.signing' version '1.1.4'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}
apply plugin: 'org.spongepowered.mixin'

def isCI = System.getenv('GITHUB_ACTION')
def nightly = System.getenv('GITHUB_NIGHTLY')
def ref = System.getenv('GITHUB_PR_NUMBER')

def tag = nightly ? 'NIGHTLY' : 'CI'
def suffix = "-${tag}" + (ref ? "-${ref}" : '')

def signProps = [:]
if (System.getenv('SECURE_SIGN')) {
    println('Using Env variables for jar signing.')
    signProps['keyStore'] = System.getenv('KEY_STORE')
    signProps['storePass'] = System.getenv('KEY_STORE_PASS')
    signProps['keyPass'] = System.getenv('KEY_STORE_KEY_PASS')
} else if (project.hasProperty('keyStore')) {
    println('Using Project properties for jar signing.')
    signProps['keyStore'] = project.getProperty('keyStore')
    signProps['storePass'] = project.getProperty('keyStorePass')
    signProps['keyPass'] = project.getProperty('keyStoreKeyPass')
}

// does in fact follow the semantic versioning. (GAMEVER)-(MAJOR.MINOR.PATCH)[-[NIGHTLY | CI]][-RUNID.RUNINT]-HASH
version = "${minecraft_version}-${mod_version}"
if (isCI) {
    version = version + suffix
}
if (isCI || nightly) {
    version = version + '-' + 'git rev-parse --short HEAD'.execute().text.trim()
}

group = 'mods.railcraft'
base {
    archivesName = 'railcraft'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

println(repeat('*', 60))
println('Project Version: ' + version)
if (isCI) {
    println("Running in Github Workflow ${tag}" + (ref ? " pull request ${ref}." : '.'))
}
if (!signProps.isEmpty()) {
    println('With .jar signing.')
}
println('Java: ' + System.getProperty('java.version'))
println('JVM: ' + System.getProperty('java.vm.version') + ' (' + System.getProperty('java.vendor') + ')')
println('Arch: ' + System.getProperty('os.arch'))
println(repeat('*', 60))

sourceSets {
    api {
        // API has no resources
        resources.srcDirs = []
    }
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        resources {
            srcDir 'src/generated/resources'
        }
    }
    test {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

configurations {
    apiImplementation.extendsFrom(implementation)
    apiRuntimeOnly.extendsFrom(runtimeOnly)
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version

    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'railcraft'

            args '--mixin', 'railcraft.mixins.json'

            mods {
                railcraft {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.test
                }
            }
        }

        client {
            workingDirectory project.file('run/client')
        }

        server {
            workingDirectory project.file('run/server')
        }

        data {
            workingDirectory project.file('run/data')

            args '--mod', 'railcraft', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }

        gameTestServer {
            workingDirectory project.file('run/test')
        }
    }
}

repositories {
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/repository/maven-public'
    }
    maven {
        name 'JEI'
        url 'https://maven.blamejared.com/'
    }
}

dependencies {
    minecraft group: 'net.neoforged', name: 'forge', version: "${project.minecraft_version}-${project.neo_version}"

    annotationProcessor group: 'org.spongepowered', name: 'mixin', version: project.mixin_version, classifier: 'processor'
    testAnnotationProcessor group: 'org.spongepowered', name: 'mixin', version: project.mixin_version, classifier: 'processor'

    compileOnly fg.deobf("mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${project.minecraft_version}-forge-api:${project.jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei_version}")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            mod_version: project.mod_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    from([sourceSets.api.output, sourceSets.main.output])
    manifest {
        attributes([
                'Specification-Title'       : project.name,
                'Specification-Vendor'      : 'Sm0keySa1m0n, 3divad99',
                'Specification-Version'     : '1',
                'Implementation-Title'      : project.name,
                'Implementation-Version'    : project.version,
                'Implementation-Vendor'     : 'Sm0keySa1m0n, 3divad99',
                'Implementation-Commit'     : 'git rev-parse HEAD'.execute().text.trim(), // this exists if you used git clone else its null.
                'Implementation-Timestamp'  : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'              : 'railcraft.mixins.json'
        ])
    }
    finalizedBy 'reobfJar'
}

mixin {
    add sourceSets.main, 'railcraft.refmap.json'
}

signing {
    if (!signProps.isEmpty()) {
        jars {
            sign jar
            after 'reobfJar'

            keyStore = signProps.keyStore
            storePass = signProps.storePass

            alias = 'railcraft'
            keyPass = signProps.keyPass
        }
    }
}
