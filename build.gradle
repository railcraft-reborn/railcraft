import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask
import static org.apache.commons.lang3.StringUtils.repeat

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version "$neo_gradle_version"
    id 'net.neoforged.gradle.mixin' version "$neo_gradle_version"
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '2.1.2'
    id 'me.modmuss50.mod-publish-plugin' version '0.5.1'
    id 'com.diffplug.spotless' version '6.25.0'
}

def isRelease = System.getenv('CURSEFORGE_TOKEN')
if (!isRelease) {
    mod_version += '-snapshot'
}

version = "$minecraft_version-$mod_version"

group = 'mods.railcraft'
base {
    archivesName = 'railcraft-reborn'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        if (System.getProperty('idea.active')) {
            vendor = JvmVendorSpec.JETBRAINS
        }
    }
}

println(repeat('*', 60))
println("Project Version: $version")
println("Java: ${System.getProperty('java.version')}")
println("JVM: ${System.getProperty('java.vm.version')} (${System.getProperty('java.vendor')})")
println("Arch: ${System.getProperty('os.arch')}")
println(repeat('*', 60))

sourceSets {
    api {
        // API has no resources
        resources.srcDirs = []
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
    main {
        compileClasspath += api.output
        runtimeClasspath += api.output
        resources {
            srcDir 'src/generated/resources'
            exclude '.cache'
        }
    }
    test {
        compileClasspath += api.output
        runtimeClasspath += api.output
    }
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        systemProperty 'neoforge.enabledGameTestNamespaces', 'railcraft'

        modSource project.sourceSets.api
        modSource project.sourceSets.main
        modSource project.sourceSets.test

        if (System.getProperty('idea.active')) {
            jvmArguments.add("-XX:+AllowEnhancedClassRedefinition")
        }
    }

    client {
    }

    server {
        programArgument '--nogui'
    }

    gameTestServer {
    }

    data {
        programArguments.addAll '--mod', 'railcraft', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

repositories {
    maven {
        name 'JEI/Patchouli'
        url 'https://maven.blamejared.com/'
    }
    maven {
        name 'curseforge'
        url 'https://www.cursemaven.com'
    }
    maven {
        name 'TerraformersMC'
        url 'https://maven.terraformersmc.com/'
    }
}

dependencies {
    implementation group: 'net.neoforged', name: 'neoforge', version: neo_version

    testImplementation platform("org.junit:junit-bom:$jupiter_version")
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly "mezz.jei:jei-$minecraft_version-common-api:$jei_version"
    compileOnly "mezz.jei:jei-$minecraft_version-neoforge-api:$jei_version"
    runtimeOnly "mezz.jei:jei-$minecraft_version-neoforge:$jei_version"

    compileOnly "dev.emi:emi-neoforge:$emi_version+$minecraft_version:api"
    runtimeOnly "dev.emi:emi-neoforge:$emi_version+$minecraft_version"

    compileOnly "vazkii.patchouli:Patchouli:$patchouli_version:api"
    runtimeOnly "vazkii.patchouli:Patchouli:$patchouli_version"

    compileOnly "curse.maven:jade-api-324717:$jade_id_api"
    runtimeOnly "curse.maven:jade-324717:$jade_id"

}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

spotless {
  java {
    target 'src/*/java/**/*.java'
    importOrder('\\#', 'java|javax|org|com|')

    endWithNewline()
    trimTrailingWhitespace()
    removeUnusedImports()
    indentWithSpaces(2)
    custom 'noWildcardImports', { String fileContents ->
        if (fileContents.contains('*;\n')) {
            throw new GradleException('No wildcard imports are allowed!')
        }
    }
    bumpThisNumberIfACustomStepChanges(1)
  }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['-Xmaxerrs', '100000'])
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
        minecraft_version       : minecraft_version,
        minecraft_version_range : minecraft_version_range,
        neo_version             : neo_version,
        neo_version_range       : neo_version_range,
        loader_version_range    : loader_version_range,
        mod_license             : mod_license,
        mod_version             : mod_version,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    from([sourceSets.api.output, sourceSets.main.output])
    manifest {
        attributes([
            'Specification-Title'       : project.name,
            'Specification-Vendor'      : 'Sm0keySa1m0n, Edivad99',
            'Specification-Version'     : '1',
            'Implementation-Title'      : project.name,
            'Implementation-Version'    : project.version,
            'Implementation-Vendor'     : 'Sm0keySa1m0n, Edivad99',
            'Implementation-Commit'     : 'git rev-parse HEAD'.execute().text.trim(),
        ])
    }
}

tasks.register('apiJar', Jar) {
    group = 'build'
    from sourceSets.api.output
    archiveClassifier = 'api'
    manifest.from(jar.manifest)
}

artifacts {
    archives jar
    archives apiJar
}

mixin {
    config 'railcraft.mixins.json'
}

def penultimateTag = { ->
    def stdout = new ByteArrayOutputStream()
    try {
        project.exec { ExecSpec spec ->
            spec.commandLine('git', 'describe', '--abbrev=0', '--tags', 'HEAD~').standardOutput(stdout)
        }.assertNormalExitValue().rethrowFailure()
    } catch (Exception e) {
        throw new IllegalStateException("Failed to get the tag; check if 'git' is on the PATH", e)
    }
    return stdout.toString()
}

tasks.register('makeChangelog', GitChangelogTask) {
    fromRepo = projectDir.absolutePath.toString()
    fromRevision = penultimateTag()
    toRevision = 'HEAD'
    file = file('CHANGELOG.md')
    untaggedName = 'Next release'
    templateContent = file('changelog.mustache').text
    ignoreCommitsIfMessageMatches = '^.*Merge branch.*$'
}

publishMods {
    displayName = "railcraft-reborn-$minecraft_version-$mod_version"
    version = mod_version
    type = BETA

    def changelogFile = file('CHANGELOG.md')
    changelog = changelogFile.exists() ? changelogFile.text : 'CHANGELOG.md not found'

    modLoaders.add('neoforge')
    file = jar.archiveFile
    additionalFiles.from(apiJar)

    curseforge {
        projectId = '901491'
        accessToken = providers.environmentVariable('CURSEFORGE_TOKEN')
        minecraftVersions.add(minecraft_version)
    }
    modrinth {
        projectId = 'rO6kKst6'
        accessToken = providers.environmentVariable('MODRINTH_TOKEN')
        minecraftVersions.add(minecraft_version)
    }
}
